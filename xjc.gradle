configurations {
    xjc
}

dependencies {
    xjc 'com.sun.xml.bind:jaxb-xjc:2.2.4-1'
}

task xjc {
    inputs.dir "src/main/resources/xjc/medline/"
    inputs.dir "src/main/resources/xjc/bibtexml/"
    inputs.dir "src/main/resources/xjc/mods/"
    outputs.dir "src/main/gen/net/sf/jabref/logic/importer/fileformat/medline"
    outputs.dir "src/main/gen/net/sf/jabref/logic/importer/fileformat/bibtexml"
    outputs.dir "src/main/gen/net/sf/jabref/logic/importer/fileformat/mods"

    ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask', classpath: configurations.xjc.asPath)

    doLast {
        ant.xjc(destdir: 'src/main/gen/', package: 'net.sf.jabref.logic.importer.fileformat.medline') {
            schema(dir: 'src/main/resources/xjc/medline', includes: 'medline.xsd')
        }
        ant.xjc(destdir: 'src/main/gen/', package: 'net.sf.jabref.logic.importer.fileformat.bibtexml') {
            schema(dir: 'src/main/resources/xjc/bibtexml', includes: 'bibtexml.xsd')
        }
        ant.xjc(destdir: 'src/main/gen/', package: 'net.sf.jabref.logic.importer.fileformat.mods') {
            arg(value: '-npa') //don't create package-info.java because it was manually added in src/main/java to ensure the namespace prefix mapping
            schema(dir: 'src/main/resources/xjc/mods', includes: 'mods-3-6.xsd')
            binding(dir: 'src/main/resources/xjc/mods', includes: 'mods-binding.xjb')
        }
    }
}

tasks.compileJava.dependsOn xjc
